plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
}

apply from: "$project.rootDir/scripts/ktlint.gradle"
apply from: "$project.rootDir/scripts/versioning.gradle"

android {
    compileSdkVersion sdkCompileVersion
    buildToolsVersion buildToolsAllVersion

    defaultConfig {
        applicationId "com.p2p.wallet"
        minSdkVersion sdkMinVersion
        targetSdkVersion sdkTargetVersion
        versionCode VERSION_CODE
        versionName VERSION_NAME

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk { abiFilters("armeabi-v7a", "x86_64", "arm64-v8a", "x86") }
    }

    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                } else {
                    println 'Unable to read signing.properties, using defaults'
                }
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            versionNameSuffix ".$VERSION_BUILD-debug"
        }

        feature {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            versionNameSuffix ".$VERSION_BUILD"

            firebaseAppDistribution {
                releaseNotes = developChangelog()
                groups = "wowlet.test.feature"
            }
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation project(path: ':sdk')

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation 'androidx.activity:activity-ktx:1.2.2'
    implementation 'androidx.fragment:fragment-ktx:1.3.2'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation 'com.google.android:flexbox:2.0.1'

    def coroutinesVersion = '1.4.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:4.7.1"
    // https://github.com/dm77/barcodescanner
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'

    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    def okHttpVersion = '4.8.1' //  TODO: migrate to stable version when released
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Debug drawer
    def debugDrawerVersion = '0.8.0'
    implementation "io.palaima.debugdrawer:debugdrawer:$debugDrawerVersion"
    implementation "io.palaima.debugdrawer:debugdrawer-base:$debugDrawerVersion"
    implementation "io.palaima.debugdrawer:debugdrawer-commons:$debugDrawerVersion"
    implementation "io.palaima.debugdrawer:debugdrawer-timber:$debugDrawerVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.6'
    kapt "androidx.room:room-compiler:2.2.6"

    // seed value
    implementation 'com.github.bitcoinj:bitcoinj:0.15.8'

    def koinVersion = '2.1.6' // TODO: migrate to 2.2.3 or 3.0.X when released
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-scope:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"

    // Indicator circle pager fixme: remove unnecessary library
    implementation "me.relex:circleindicator:2.1.4"
    implementation "ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.2.1"

    // code input fixme: remove unnecessary library
    implementation "com.raycoarana.codeinputview:codeinputview:2.1.4"

    // MPAndroidChart fixme: remove unnecessary library
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // qrcode fixme: remove unnecessary library
    implementation "me.dm7.barcodescanner:zbar:1.9.13"

    // generate qr code
    implementation 'com.github.kenglxn.QRGen:android:2.6.0'
}